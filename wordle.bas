10 CLS
20 PRINT "  GUESS THE %W%O%R%D%L%E IN 6 TRIES"
30 PRINT
40 PRINT "EACH GUESS MUST BE A FIVE-LETTER"
50 PRINT "WORD. HIT ENTER BUTTON TO SUBMIT"
60 PRINT
70 PRINT "        AFTER EACH GUESS,"
80 PRINT "     A STRING CONSISTING OF"
90 PRINT "CHARACTERS, INVERTED CHARACTERS,"
100 PRINT "AND DOTS WILL APPEAR TO SHOW HOW"
110 PRINT "CLOSE YOUR GUESS WAS TO THE WORD"
120 PRINT
130 PRINT "EXAMPLE FOR THE WORD %M%O%U%S%E" 
140 PRINT
150 PRINT "SHAKE > S...%E"
160 PRINT
170 PRINT " S - WORD CONTAINS THE LETTER"
180 PRINT " . - LETTER NOT IN WORD"
190 PRINT " %E - LETTER CORRECT POSITION"
200 PRINT
210 PRINT "    PRESS ANY KEY TO START."
220 IF INKEY$ = "" THEN GOTO 220
230 CLS
300 RAND
310 LET A$="HELLOWORLDBASICWORDSTHINKAPPLEFIRSTMOUSERADIOPOWERSWEETBROWNSHAKEROBOTPRINTDIODE"
320 LET B=(INT(RND*(LEN(A$)/5))*5)+1
330 LET B$=A$(B TO B+4)
340 LET D=1
350 PRINT "WHAT IS THE WORD?"
355 PRINT
360 INPUT C$
370 IF LEN(C$) <> 5 THEN GOTO 360
380 PRINT C$;" > ";
400 FOR N = 1 TO 5
410 IF C$(N) <> B$(N) THEN GOTO 440
420 PRINT CHR$(CODE(C$(N))+128);
430 GOTO 500
440 FOR M = 1 TO 5
450 IF C$(N) <> B$(M) THEN GOTO 480
460 PRINT C$(N);
470 GOTO 500
480 NEXT M
490 PRINT ".";
500 NEXT N
510 PRINT
520 IF B$=C$ OR D>5 THEN GOTO 600
530 LET D=D+1
540 GOTO 360
600 PRINT "GAME OVER"
610 STOP
